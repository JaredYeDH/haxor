section .data
welcome_msg:      dw "Simple 'Guess the number' game. Powered by Haxor VM.", 10, 0
generate_msg_1:   dw "I will pick a number between ", 0
generate_msg_2:   dw " and ", 0
generate_msg_3:   dw ", your job is to guess it in smallest number of tries.", 10, 0
question_msg:     dw 10, "What is the number? ", 0
win_msg_1:        dw "You guessed my number after ", 0
win_msg_2:        dw " tries!", 10, 0
bigger_msg:       dw "I'm sorry, but my number is bigger, try again.", 10, 0
smaller_msg:      dw "I'm sorry, but my number is smaller, try again.", 10, 0
incorrect_msg:    dw "You must provide a number!", 10, 0

range_min:        dw 1
range_max:        dw 1000

section .bss
number:           dw
answer:           dw
tries:            dw

section .text

win:
  pushi win_msg_1
  addi $sc, $zero, 01h
  syscall
  addi $sp, $sp, 8

  pushm tries
  addi $sc, $zero, 02h
  syscall
  addi $sp, $sp, 8

  pushi win_msg_2
  addi $sc, $zero, 01h
  syscall
  addi $sp, $sp, 8

  exiti 0

bigger:
  pushi bigger_msg
  addi $sc, $zero, 01h
  syscall
  addi $sp, $sp, 8

  j question

smaller:
  pushi smaller_msg
  addi $sc, $zero, 01h
  syscall
  addi $sp, $sp, 8

  j question

main:
  pushi welcome_msg
  addi $sc, $zero, 01h
  syscall
  addi $sp, $sp, 8

  pushi generate_msg_1
  addi $sc, $zero, 01h
  syscall
  addi $sp, $sp, 8

  pushm range_min
  addi $sc, $zero, 02h
  syscall
  addi $sp, $sp, 8

  pushi generate_msg_2
  addi $sc, $zero, 01h
  syscall
  addi $sp, $sp, 8

  pushm range_max
  addi $sc, $zero, 02h
  syscall
  addi $sp, $sp, 8

  pushi generate_msg_3
  addi $sc, $zero, 01h
  syscall
  addi $sp, $sp, 8

generate:
  pushm range_max
  pushm range_min
  addi $sc, $zero, 05h
  syscall
  addi $sp, $sp, 16
  sw $zero, number, $sc

  # set tries to 0
  sw $zero, tries, $zero

question:
  pushi question_msg
  addi $sc, $zero, 01h
  syscall
  addi $sp, $sp, 8

  pushi answer
  addi $sc, $zero, 04h
  syscall
  addi $sp, $sp, 8

  bgtz $sc, correct

  pushi incorrect_msg
  addi $sc, $zero, 01h
  syscall
  addi $sp, $sp, 8
  j question

correct:
  lw $t1, $zero, tries
  addi $t1, $t1, 1
  sw $0, tries, $t1

compare:
  lw $t0, $zero, number
  lw $t1, $zero, answer
  beq $t0, $t1, win
  bgt $t0, $t1, bigger
  blt $t0, $t1, smaller
